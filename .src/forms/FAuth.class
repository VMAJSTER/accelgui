' Gambas class file

Private intX As Integer
Private intY As Integer

Public Sub btnExit_Click()

    Me.Close()

End

Public Sub pctClose_MouseDown()

    btnExit_Click()

End

Public Sub btnEnter_Click()

    Dim strSavedPass As String
    Dim strEnteredPass As String

    strSavedPass = MMain.objSettings["Auth/" & cmbUsername.Text, ""]
    strEnteredPass = Crypt.SHA512(txbPassword.Text, MMain.PREFIX)
    If strSavedPass <> "" Then
        If strEnteredPass = strSavedPass Then
            PassOk()
        Else
            PassError()
        Endif
    Else
        If txbPassword.Text = cmbUsername.Text Then
            PassOk()
        Else
            PassError()
        Endif
    Endif

End

Private Sub PassOk()

    MMain.strCurrentUser = cmbUsername.Text
    Me.Close()
    FMain.Show()

End


Private Sub PassError()

    Message.Title = ("Authorization error")
    Message.Error(("Password you entered is incorrect"))
    txbPassword.Clear()
    txbPassword.SetFocus()

End

Public Sub pnlTitle_MouseDown()

    intX = Mouse.X + pnlTitle.Left
    intY = Mouse.Y + pnlTitle.Top

End

Public Sub pnlTitle_MouseMove()

    Dim intLeft As Integer
    Dim intTop As Integer

    If Mouse.Left Then
        intLeft = Mouse.ScreenX - intX
        intTop = Mouse.ScreenY - intY
        If intLeft >= 0 And intTop >= 0 Then
            If intLeft + Me.W <= Screen.W Then
                If intTop + Me.H <= Screen.H Then
                    Me.Left = intLeft
                    Me.Top = intTop
                Endif
            Endif
        Endif
    Endif

End

Public Sub lblTitle_MouseDown()

    pnlTitle_MouseDown()

End

Public Sub lblTitle_MouseMove()

    pnlTitle_MouseMove()

End

Public Sub Form_Open()

    Dim bytDefault As Byte

    Me.Center()
    bytDefault = CByte(MMain.objSettings["Auth/default", "0"])
    cmbUsername.Index = bytDefault

End
