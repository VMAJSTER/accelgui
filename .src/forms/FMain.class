' Gambas class file

Public Sub Form_Resize()

    With pnlTitle
        .W = Me.ClientW - MMain.MARGIN * 2
        .Left = MMain.MARGIN
        .Top = MMain.MARGIN
    End With

    With lblTitle
        .W = pnlTitle.W - pctClose.W - MMain.MARGIN * 2
        .Left = MMain.MARGIN
        .Top = MMain.MARGIN / 2
    End With

    With pctClose
        .Left = pnlTitle.W - .W
        .Top = 0
    End With

    With pnlToolBar
        .W = Me.ClientW - MMain.MARGIN * 2
        .H = MMain.TLB_HEIGHT
        .Left = MMain.MARGIN
        .Top = pnlTitle.H + MMain.MARGIN
    End With

    With wksServers
        .W = Me.ClientW - MMain.MARGIN * 2
        .H = Me.ClientH - pnlTitle.H - pnlToolBar.H - MMain.MARGIN * 2
        .Left = MMain.MARGIN
        .Top = pnlTitle.H + pnlToolBar.H + MMain.MARGIN
    End With

End

Public Sub tlbServers_Click()

    FServers.ShowModal()

End

Public Sub pctClose_MouseDown()

    FMain.Close()

End

Public Sub Form_Open()

    Dim i As Integer
    Dim arrStatus As New String[]
    Dim arrName As New String[]
    Dim arrAddress As New String[]
    Dim arrPort As New String[]
    Dim objForm As FSessions

    arrStatus = Split(MMain.objSettings["Servers/Status"], ";")
    arrName = Split(MMain.objSettings["Servers/Name"], ";")
    arrAddress = Split(MMain.objSettings["Servers/Address"], ";")
    arrPort = Split(MMain.objSettings["Servers/Port"], ";")
    For i = 0 To arrAddress.Max
        If arrStatus[i] = "True" Then
            objForm = New FSessions(arrName[i], arrAddress[i], arrPort[i])
            wksServers.Add(objForm)
        Endif
    Next
    If MMain.strCurrentUser = "user" Or If MMain.strCurrentUser = "guest" Then
        tlbReboot.Enabled = False
        tlbShutdown.Enabled = False
        tlbSettings.Enabled = False
    Endif
    If MMain.strCurrentUser = "guest" Then
        tlbKillGroup.Enabled = False
        tlbKillOne.Enabled = False
    Endif

End

Public Sub tlbUpdate_Click()

    Dim objWindow As FSessions

    If wksServers.Windows.Count > 0 Then
        objWindow = wksServers.ActiveWindow
        objWindow.objServer.GetSessions()
    Endif

End

Public Sub tlbSettings_Click()

    FSettings.ShowModal()

End
