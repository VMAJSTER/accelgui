' Gambas class file

Private intX As Integer
Private intY As Integer

Public Sub Form_Open()

    Me.Center()

End

Public Sub btnExit_Click()

    Me.Close()

End

Public Sub pctClose_MouseDown()

    btnExit_Click()

End

Public Sub pnlTitle_MouseDown()

    intX = Mouse.X + pnlTitle.Left
    intY = Mouse.Y + pnlTitle.Top

End

Public Sub pnlTitle_MouseMove()

    Dim intLeft As Integer
    Dim intTop As Integer

    If Mouse.Left Then
        intLeft = Mouse.ScreenX - intX
        intTop = Mouse.ScreenY - intY
        If intLeft >= 0 And intTop >= 0 Then
            If intLeft + Me.W <= Screen.W Then
                If intTop + Me.H <= Screen.H Then
                    Me.Left = intLeft
                    Me.Top = intTop
                Endif
            Endif
        Endif
    Endif

End

Public Sub lblTitle_MouseDown()

    pnlTitle_MouseDown()

End

Public Sub lblTitle_MouseMove()

    pnlTitle_MouseMove()

End


Public Sub btnReset_Click()

    txbAdmin.Text = "admin"
    txbUser.Text = "user"
    txbGuest.Text = "guest"
    rdbAdmin.Value = True

End

Public Sub btnSave_Click()

    Dim objControl As Control
    Dim objRadio As RadioButton

    ' It is very useful to save last copy of config file as a backup
    If Exist(MMain.GLOBAL_CONFIG) Then
        If Exist(MMain.GLOBAL_CONFIG_BACKUP) Then Kill MMain.GLOBAL_CONFIG_BACKUP
        Copy MMain.GLOBAL_CONFIG To MMain.GLOBAL_CONFIG_BACKUP
    Endif
    ' Save encrypted passwords if the appropriate text fields are filled in
    If Trim(txbAdmin.Text) <> "" Then
        MMain.objSettings["Auth/admin"] = Crypt.SHA512(txbAdmin.Text, MMain.PREFIX)
    Endif
    If Trim(txbUser.Text) <> "" Then
        MMain.objSettings["Auth/user"] = Crypt.SHA512(txbUser.Text, MMain.PREFIX)
    Endif
    If Trim(txbGuest.Text) <> "" Then
        MMain.objSettings["Auth/guest"] = Crypt.SHA512(txbGuest.Text, MMain.PREFIX)
    Endif
    For Each objControl In Me.Controls
        If objControl Is RadioButton Then
            objRadio = objControl
            If objRadio.Value Then
                MMain.objSettings["Auth/default"] = objRadio.Tag
                Break
            Endif
        Endif
    Next
    Me.Close()

End