' Gambas class file

Private intX As Integer
Private intY As Integer

Public Sub _new()

    Dim i As Byte

    grvServers.Columns.Count = 5
    grvServers.Columns[0].Width = 25
    grvServers.Columns[0].Text = ("P")
    grvServers.Columns[1].Width = 25
    grvServers.Columns[1].Text = ("S")
    grvServers.Columns[2].Width = 250
    grvServers.Columns[2].Text = ("Name")
    grvServers.Columns[3].Width = 125
    grvServers.Columns[3].Text = ("Address")
    grvServers.Columns[4].Width = 75
    grvServers.Columns[4].Text = ("Port")
    For i = 0 To 4
        grvServers.Columns[i].Alignment = Align.Center
    Next

End

Public Sub Form_Open()

    Dim i, j As Integer
    Dim arrStatus As New String[]
    Dim arrName As New String[]
    Dim arrAddress As New String[]
    Dim arrPort As New String[]

    grvServers.Clear()
    grvServers.Rows.Count = 0
    ShowRowNum(0)
    MMain.EnableControls(False)
    arrStatus = Split(MMain.objSettings["Servers/Status"], ";")
    arrName = Split(MMain.objSettings["Servers/Name"], ";")
    arrAddress = Split(MMain.objSettings["Servers/Address"], ";")
    arrPort = Split(MMain.objSettings["Servers/Port"], ";")
    For i = 0 To arrAddress.Max
        Inc grvServers.Rows.Count
        grvServers[i, 0].Picture = Picture["images/stop.png"]
        For j = 0 To FMain.wksServers.Windows.Max
            If FMain.wksServers.Windows[j].Text = arrName[i] Then
                grvServers[i, 0].Picture = Picture["images/start.png"]
            Endif
        Next
        If arrStatus[i] = "True" Then
            grvServers[i, 1].Picture = Picture["images/enable.png"]
        Else
            grvServers[i, 1].Picture = Picture["images/disable.png"]
        Endif
        grvServers[i, 2].Text = arrName[i]
        grvServers[i, 3].Text = arrAddress[i]
        grvServers[i, 4].Text = arrPort[i]
    Next
    If grvServers.Rows.Count > 0 Then
        MMain.ActivateRow(grvServers, 0)
        MMain.EnableControls(True)
    Endif
    If MMain.strCurrentUser = "admin" And If User.Id = 0 Then
        tlbAdd.Enabled = True
        tlbEdit.Enabled = True
        tlbDelete.Enabled = True
        tlbClear.Enabled = True
        tlbCheck.Enabled = True
        tlbSave.Enabled = True
    Endif

End

Public Sub tlbAdd_Click()

    With FSrvAction
        .bolEdit = False
        .lblTitle.Text = ("Add server")
        .ShowModal()
    End With

End

Public Sub tlbEdit_Click()

    With FSrvAction
        If grvServers.Row > -1 Then
            .bolEdit = True
            .lblTitle.Text = ("Edit server")
            .ShowModal()
        Endif
    End With

End

Public Sub tlbDelete_Click()

    Dim intRow As Integer

    If grvServers.Row > -1 Then
        Message.Title = ("Delete server")
        If Message.Question(("Are you sure to delete server?"), ("Yes"), ("No")) = 1 Then
            intRow = grvServers.Row
            grvServers.Rows.Remove(intRow)
            If intRow = grvServers.Rows.Count Then Dec intRow
            If grvServers.Rows.Count > 0 Then
                MMain.ActivateRow(grvServers, intRow)
            Endif
            ShowRowNum(intRow + 1)
            If grvServers.Rows.Count = 0 Then MMain.EnableControls(False)
        Endif
    Endif

End

Public Sub tlbClear_Click()

    Message.Title = ("Clear servers list")
    If Message.Question(("Are you sure to clear list of servers?"), ("Yes"), ("No")) = 1 Then
        grvServers.Clear()
        grvServers.Rows.Count = 0
        ShowRowNum(0)
        MMain.EnableControls(False)
    Endif

End

Public Sub tlbFirst_Click()

    If grvServers.Rows.Count > 0 Then
        MMain.ActivateRow(grvServers, 0)
        ShowRowNum(1)
    Endif

End

Public Sub tlbPrevious_Click()

    If grvServers.Row > 0 Then
        MMain.ActivateRow(grvServers, grvServers.Row - 1)
        ShowRowNum(grvServers.Row + 1)
    Endif

End

Public Sub tlbNext_Click()

    If grvServers.Row < grvServers.Rows.Max Then
        MMain.ActivateRow(grvServers, grvServers.Row + 1)
        ShowRowNum(grvServers.Row + 1)
    Endif

End

Public Sub tlbLast_Click()

    If grvServers.Rows.Count > 0 Then
        MMain.ActivateRow(grvServers, grvServers.Rows.Max)
        ShowRowNum(grvServers.Rows.Count)
    Endif

End

Public Sub tlbCheck_Click()

    If grvServers.Row > -1 Then
        If grvServers[grvServers.Row, 1].Picture = Picture["images/enable.png"] Then
            grvServers[grvServers.Row, 1].Picture = Picture["images/disable.png"]
            tlbCheck.Picture = Picture["images/enable.png"]
        Else
            grvServers[grvServers.Row, 1].Picture = Picture["images/enable.png"]
            tlbCheck.Picture = Picture["images/disable.png"]
        Endif
    Endif

End

Public Sub tlbSave_Click()

    Dim i As Integer
    Dim arrStatus As New String[]
    Dim arrName As New String[]
    Dim arrAddress As New String[]
    Dim arrPort As New String[]

    For i = 0 To grvServers.Rows.Max
        If grvServers[i, 1].Picture = Picture["images/enable.png"] Then
            arrStatus.Add("True")
        Else
            arrStatus.Add("False")
        Endif
        arrName.Add(grvServers[i, 2].Text)
        arrAddress.Add(grvServers[i, 3].Text)
        arrPort.Add(grvServers[i, 4].Text)
    Next
    MMain.objSettings["Servers/Status"] = arrStatus.Join(";")
    MMain.objSettings["Servers/Name"] = arrName.Join(";")
    MMain.objSettings["Servers/Address"] = arrAddress.Join(";")
    MMain.objSettings["Servers/Port"] = arrPort.Join(";")

End

Public Sub grvServers_Select()

    If grvServers.Row > -1 Then
        ShowRowNum(grvServers.Row + 1)
        If grvServers[grvServers.Row, 1].Picture = Picture["images/disable.png"] Then
            tlbCheck.Picture = Picture["images/enable.png"]
        Else
            tlbCheck.Picture = Picture["images/disable.png"]
        Endif
        If grvServers[grvServers.Row, 0].Picture = Picture["images/start.png"] Then
            tlbAction.Picture = Picture["images/stop.png"]
            tlbAction.Tooltip = ("Stop")
        Else
            tlbAction.Picture = Picture["images/start.png"]
            tlbAction.Tooltip = ("Start")
        Endif
    Endif

End

Public Sub pctClose_MouseDown()

    Me.Close()

End

Public Sub grvServers_DblClick()

    tlbEdit_Click()

End

Public Sub tlbAction_Click()

    Dim objForm As FSessions
    Dim strName As String
    Dim strHost As String
    Dim intPort As Integer
    Dim objWindow As Window

    If grvServers.Row > -1 Then
        If grvServers[grvServers.Row, 0].Picture = Picture["images/start.png"] Then
            grvServers[grvServers.Row, 0].Picture = Picture["images/stop.png"]
            tlbAction.Picture = Picture["images/start.png"]
            tlbAction.Tooltip = ("Start")
            For Each objWindow In FMain.wksServers.Windows
                If objWindow.Text = grvServers[grvServers.Row, 2].Text Then
                    objWindow.Close()
                Endif
            Next
        Else
            grvServers[grvServers.Row, 0].Picture = Picture["images/start.png"]
            tlbAction.Picture = Picture["images/stop.png"]
            tlbAction.Tooltip = ("Stop")
            strName = grvServers[grvServers.Row, 2].Text
            strHost = grvServers[grvServers.Row, 3].Text
            intPort = CInt(grvServers[grvServers.Row, 4].Text)
            objForm = New FSessions(strName, strHost, intPort)
            FMain.wksServers.Add(objForm)
        Endif
    Endif

End

Public Sub pnlTitle_MouseDown()

    intX = Mouse.X + pnlTitle.Left
    intY = Mouse.Y + pnlTitle.Top

End

Public Sub pnlTitle_MouseMove()

    Dim intLeft As Integer
    Dim intTop As Integer

    If Mouse.Left Then
        intLeft = Mouse.ScreenX - intX
        intTop = Mouse.ScreenY - intY
        If intLeft >= 0 And intTop >= 0 Then
            If intLeft + Me.W <= Screen.W Then
                If intTop + Me.H <= Screen.H Then
                    Me.Left = intLeft
                    Me.Top = intTop
                Endif
            Endif
        Endif
    Endif

End

Public Sub lblTitle_MouseDown()

    pnlTitle_MouseDown()

End

Public Sub lblTitle_MouseMove()

    pnlTitle_MouseMove()

End

Private Sub ShowRowNum(intCur As Integer)

    lblSrvCount.Text = CStr(intCur) & " / " & CStr(grvServers.Rows.Count)

End
