' Gambas class file


' ---------
' Constants
' ---------
Private Const CMD_SESSIONS As String = "show sessions"
Private Const CMD_RELOAD As String = "reload"
Private Const CMD_SHUTDOWN As String = "shutdown"
Private Const CMD_KILL As String = "terminate"
Private Const CMD_RESTART As String = "restart"
Private Const CMD_CHAPER_CHANGE As String = "shaper change"
Private Const CMD_CHAPER_RESTORE As String = "shaper restore"


' ---------------------
' Fields default values
' ---------------------
Private Const DFL_NAME As String = "accel-ppp"
Private Const DFL_HOST As String = "127.0.0.1"
Private Const DFL_PORT As Integer = 2001
Private Const DFL_MATCH As String = "active"


' ---------
' Variables
' ---------
Private objSrv As Socket
Private arrSessionsData As String[]


' ------
' Fields
' ------
Private strName As String = DFL_NAME
Private strHost As String = DFL_HOST
Private intPort As Integer = DFL_PORT
Private bolConnected As Boolean
Private bolConnecting As Boolean
Private arrSessions As CSession[]
Private strShutMode As String = MShutMode.Default
Private strOrderMode As String = MColumn.UserName
Private strMatchMode As String = MColumn.State
Private strMatchValue As String = DFL_MATCH
Private strColumn As String = MColumn.Default
Private strTermMode As String = MTermMode.Soft


' ----------
' Properties
' ----------
Property Name As String
Property Host As String
Property Port As Integer
Property Read Connected As Boolean
Property Read Connecting As Boolean
Property Read Sessions As CSession[]
Property ShutMode As String
Property OrderMode As String
Property MatchMode As String
Property MatchValue As String
Property Column As String
Property TermMode As String


' ------
' Events
' ------
Event Connected
Event Disconnected
Event GotSessions
Event Reloaded
Event Shutdown
Event Killed
Event Restarted
Event Changed
Event Restored

' ------------------------------
' Properties setters and getters
' ------------------------------

Private Function Name_Read() As String

    Return strName

End

Private Sub Name_Write(Value As String)

    Value = Trim(Value)
    If Value = Null Then Value = DFL_NAME
    strName = Value

End

Private Function Host_Read() As String

    Return strHost

End

Private Sub Host_Write(Value As String)

    Value = Trim(Value)
    If Value = Null Or If Not MMain.IsIpAddress(Value) Then Value = DFL_HOST
    strHost = Value

End

Private Function Port_Read() As Integer

    Return intPort

End

Private Sub Port_Write(Value As Integer)

    If Value < 1 Or Value > 65535 Then Value = DFL_PORT
    intPort = Value

End

Private Function Connected_Read() As Boolean

    Return bolConnected

End

Private Function Connecting_Read() As Boolean

    Return bolConnecting

End

Private Function Sessions_Read() As CSession[]

    Return arrSessions

End

Private Function ShutMode_Read() As String

    Return strShutMode

End

Private Sub ShutMode_Write(Value As String)

    Value = Trim(Value)
    If MShutMode.Mode.Exist(Value) Then
        strShutMode = Value
    Else
        strShutMode = MShutMode.Default
    Endif

End

Private Function OrderMode_Read() As String

    Return strOrderMode

End

Private Sub OrderMode_Write(Value As String)

    Value = Trim(Value)
    If MColumn.Columns.Exist(Value) Then
        strOrderMode = Value
    Else
        strOrderMode = MColumn.Default
    Endif

End

Private Function MatchMode_Read() As String

    Return strMatchMode

End

Private Sub MatchMode_Write(Value As String)

    Value = Trim(Value)
    If MColumn.Columns.Exist(Value) Then
        strMatchMode = Value
    Else
        strMatchMode = MColumn.Default
    Endif

End

Private Function MatchValue_Read() As String

    Return strMatchValue

End

Private Sub MatchValue_Write(Value As String)

    strMatchValue = Value

End

Private Function Column_Read() As String

    Return strColumn

End

Private Sub Column_Write(Value As String)

    Value = Trim(Value)
    If MColumn.Columns.Exist(Value) Then
        strColumn = Value
    Else
        strColumn = MColumn.Default
    Endif

End

Private Function TermMode_Read() As String

    Return strTermMode

End

Private Sub TermMode_Write(Value As String)

    Value = Trim(Value)
    If MTermMode.Mode.Exist(Value) Then
        strShutMode = Value
    Else
        strShutMode = MTermMode.Default
    Endif

End


' -----------
' Constructor
' -----------

Public Sub _new()

    objSrv = New Socket As "objSrv"
    arrSessionsData = New String[]
    arrSessions = New CSession[]

End

' -----------
' Desctructor
' -----------

Public Sub _free()

    Disconnect()

End


' -------
' Methods
' -------

Public Sub Connect()

    If Not bolConnected And If Not bolConnecting Then
        objSrv.Connect(strHost, intPort)
        bolConnecting = True
    Endif

End

Public Sub Disconnect()

    If bolConnected Or bolConnecting Then objSrv.Close()

End

Public Sub Reload()

    If bolConnected Then SendData(CMD_RELOAD)

End

Public Sub Shutdown()

    If bolConnected Then
        If strShutMode = MShutMode.Default Then
            SendData(CMD_SHUTDOWN)
        Else
            SendData(CMD_SHUTDOWN & " " & strShutMode)
        Endif
    Endif

End

Public Sub GetSessions()

    Dim strLineToSend As String

    If bolConnected Then
        strLineToSend = CMD_SESSIONS
        If strColumn <> MColumn.Default Then strLineToSend &= " " & strColumn
        If strOrderMode <> MColumn.Default Then strLineToSend &= " order " & strOrderMode
        If strMatchMode <> MColumn.Default And If strMatchValue <> Null Then
            strLineToSend &= " match " & strMatchMode & " " & strMatchValue
        Endif
        arrSessionsData.Clear()
        SendData(strLineToSend)
    Endif

End

Public Sub KillSession(Username As String)

    Dim strLineToSend As String

    If bolConnected Then
        Username = Trim(Username)
        If Username <> "" Then
            strLineToSend = CMD_KILL & " username " & Username & " " & strTermMode
            SendData(strLineToSend)
        Endif
    Endif

End

Public Sub KillAll()

    Dim strLineToSend As String

    If bolConnected Then
        strLineToSend = CMD_KILL & " all " & strTermMode
        SendData(strLineToSend)
    Endif

End

Public Sub Restart()

    If bolConnected Then
        If strTermMode = MTermMode.Soft Then
            SendData(CMD_RESTART)
        Else
            SendData(CMD_RESTART & " " & strTermMode)
        Endif
    Endif

End

Public Sub ShaperChange(strInterface As String, intIn As Integer, intOut As Integer, Optional bolTemp As Boolean)

    Dim strLineToSend As String

    If bolConnected Then
        strLineToSend = CMD_CHAPER_CHANGE & " " & strInterface & " " & Str(intIn) & "/" & Str(intOut)
        If bolTemp Then strLineToSend &= " temp"
        SendData(strLineToSend)
    Endif

End

Public Sub ShaperChangeAll(intIn As Integer, intOut As Integer, Optional bolTemp As Boolean)

    Dim strLineToSend As String

    If bolConnected Then
        strLineToSend = CMD_CHAPER_CHANGE & " all " & Str(intIn) & "/" & Str(intOut)
        If bolTemp Then strLineToSend &= " temp"
        SendData(strLineToSend)
    Endif

End

Public Sub ShaperRestore(strInterface As String)

    Dim strLineToSend As String

    If bolConnected Then
        strLineToSend = CMD_CHAPER_RESTORE & " " & strInterface
        SendData(strLineToSend)
    Endif

End

Public Sub ShaperRestoreAll()

    Dim strLineToSend As String

    If bolConnected Then
        strLineToSend = CMD_CHAPER_RESTORE & " all "
        SendData(strLineToSend)
    Endif

End


' ---------------------
' Socket event handlers
' ---------------------

Public Sub objSrv_Closed()

    bolConnected = False
    bolConnecting = False
    Raise Disconnected

End

Public Sub objSrv_Error()

    bolConnected = False
    bolConnecting = False
    Raise Disconnected

End

Public Sub objSrv_Ready()

    bolConnected = True
    bolConnecting = False
    Raise Connected

End

Public Sub objSrv_Read()

    Dim strLine As String

    For Each strline In objSrv.Lines
        arrSessionsData.Add(strline)
    Next
    ParseSessions()

End

' -------------------
' Private subroutines
' -------------------

Private Sub SendData(strCommand As String)

    Dim intCommandLength As Integer

    strCommand &= gb.Lf
    intCommandLength = Len(strCommand)
    Write #objSrv, strCommand, intCommandLength
    Select Case strCommand
        Case Like CMD_RELOAD
            Raise Reloaded
        Case Like CMD_SHUTDOWN & "*"
            Raise Shutdown
        Case Like CMD_KILL & "*"
            Raise Killed
        Case Like CMD_RESTART & "*"
            Raise Restarted
        Case Like CMD_CHAPER_CHANGE & "*"
            Raise Changed
        Case Like CMD_CHAPER_RESTORE & "*"
            Raise Restored
    End Select

End

Private Sub ParseSessions()

    Dim i As Integer
    Dim arrRecord As New String[]
    Dim arrSession As CSession
    Dim arrRate As New String[]

    If arrSessionsData.Length > 2 Then
        arrSessions.Clear()
        For i = 2 To arrSessionsData.Max
            arrRecord = Split(arrSessionsData[i], "|")
            If arrRecord.Length = 9 Then
                arrRate = Split(Trim(arrRecord[4]), "/")
                arrSession = New CSession
                With arrSession
                    .IFName = Trim(arrRecord[0])
                    .UserName = Trim(arrRecord[1])
                    .CallingSID = Trim(arrRecord[2])
                    .IP = Trim(arrRecord[3])
                    If arrRate.Count < 2 Then
                        .RateIn = 0
                        .RateOut = 0
                    Else
                        .RateIn = CInt(arrRate[0])
                        .RateOut = CInt(arrRate[1])
                    Endif
                    .Type = Trim(arrRecord[5])
                    .Comp = CBool(Trim(arrRecord[6]))
                    .State = Trim(arrRecord[7])
                    .Uptime = Trim(arrRecord[8])
                End With
                arrSessions.Add(arrSession)
            Endif
        Next
        Raise GotSessions
    Endif

End
